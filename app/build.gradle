plugins {
    id 'com.android.application'
}

android {
    compileSdk 33

    signingConfigs {
        release {
            // Load local.properties in an object
            def localProperties = new Properties()
            localProperties.load(new FileInputStream(rootProject.file("local.properties")))
            // Use the object to access properties
            storeFile file(localProperties['STORE_FILE'])
            storePassword localProperties['STORE_PASSWORD']
            keyPassword localProperties['KEY_PASSWORD']
            keyAlias localProperties['KEY_ALIAS']
        }
    }
    
    defaultConfig {
        applicationId "com.shatyuka.killergram"
        minSdk 16
        targetSdk 33
        versionCode appVerCode.toInteger()
        versionName appVerName
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "app_version", variant.versionName
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "Killergram_${defaultConfig.versionName}.apk"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
}

